OUTPUT_FORMAT("elf32-i386")
KERNEL_LOAD_ADDR = 0x00100000;
KERNEL_VIRTUAL_ADDRESS = 0xC0000000;

ENTRY(start)

PHDRS
{
	text PT_LOAD FILEHDR PHDRS;
	data PT_LOAD;
	setup PT_LOAD;
}


SECTIONS
{
	. = KERNEL_LOAD_ADDR;

	.setup ALIGN(4096) : 
	{
		*(.setup)
	} : setup
	
	. += KERNEL_VIRTUAL_ADDRESS;

	.text ALIGN(4096) : AT(ADDR(.text) - KERNEL_VIRTUAL_ADDRESS)
	{
		__KERNEL_ADDR_VIRT_CODE_START = .;
		*(.text)
		*(.rodata*)
		__KERNEL_ADDR_VIRT_CODE_END = .;
	} : text
	
	.data ALIGN(4096) : AT(ADDR(.data) - KERNEL_VIRTUAL_ADDRESS)
	{
		__KERNEL_ADDR_VIRT_DATA_START = .;
		*(.data)
		__KERNEL_ADDR_VIRT_DATA_END = .;
	} : data
	
	.bss ALIGN(4096) : AT(ADDR(.bss) - KERNEL_VIRTUAL_ADDRESS)
	{
		__KERNEL_ADDR_VIRT_BSS_START = .;
		*(COMMON*)
		*(.bss)
		__KERNEL_ADDR_VIRT_BSS_END = .;
	} : data
	

	/* Stabs debugging sections.  */
	.stab          0 : { *(.stab) }
	.stabstr       0 : { *(.stabstr) }
	.stab.excl     0 : { *(.stab.excl) }
	.stab.exclstr  0 : { *(.stab.exclstr) }
	.stab.index    0 : { *(.stab.index) }
	.stab.indexstr 0 : { *(.stab.indexstr) }
	.comment       0 : { *(.comment) }
	/* DWARF debug sections.
	Symbols in the DWARF debugging sections are relative to the beginning
	of the section so we begin them at 0.  */
	/* DWARF 1 */
	.debug          0 : { *(.debug) }
	.line           0 : { *(.line) }
	/* GNU DWARF 1 extensions */
	.debug_srcinfo  0 : { *(.debug_srcinfo) }
	.debug_sfnames  0 : { *(.debug_sfnames) }
	/* DWARF 1.1 and DWARF 2 */
	.debug_aranges  0 : { *(.debug_aranges) }
	.debug_pubnames 0 : { *(.debug_pubnames) }
	/* DWARF 2 */
	.debug_info     0 : { *(.debug_info .gnu.linkonce.wi.*) }
	.debug_abbrev   0 : { *(.debug_abbrev) }
	.debug_line     0 : { *(.debug_line) }
	.debug_frame    0 : { *(.debug_frame) }
	.debug_str      0 : { *(.debug_str) }
	.debug_loc      0 : { *(.debug_loc) }
	.debug_macinfo  0 : { *(.debug_macinfo) }
	/* SGI/MIPS DWARF 2 extensions */
	.debug_weaknames 0 : { *(.debug_weaknames) }
	.debug_funcnames 0 : { *(.debug_funcnames) }
	.debug_typenames 0 : { *(.debug_typenames) }
	.debug_varnames  0 : { *(.debug_varnames) }	
}
